prototype(Workshop.Theme:Content.Headline) < prototype(Neos.Neos:ContentComponent) {
    tagName = ${this.isFirstElement ? 'h1' : 'h2'}
    subTagName = ${'h' + (String.toInteger(String.substr(this.tagName, 1)) + 1)}

    namespace = 'headline'

    titleProperty = 'title'
    subtitleProperty = 'subtitle'
    leadProperty = null

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}

        title = Neos.Neos:Editable {
            @if.set = ${this.property}
            property = ${props.titleProperty}
            block = false
        }
        subtitle = Neos.Neos:Editable {
            @if.set = ${this.property}
            property = ${props.subtitleProperty}
            block = false
        }
        lead = Neos.Neos:Editable {
            @if.set = ${this.property}
            property = ${props.leadProperty}
            block = false
        }

        renderer = afx`
            <div class={props.namespace + '__wrapper reveals'} @if.set={props.title}>
                <Neos.Fusion:Augmenter fallbackTagName='header' class={props.namespace}>
                    <Neos.Fusion:Tag tagName={props.tagName} attributes.class={props.namespace + '__title'}>
                        {props.title}
                    </Neos.Fusion:Tag>
                    <Neos.Fusion:Tag tagName={props.subTagName} @if.set={props.subtitle} attributes.class={props.namespace + '__subtitle'}>
                        {props.subtitle}
                    </Neos.Fusion:Tag>
                    <p @if.set={props.lead} class={props.namespace + '__lead'}>
                        {props.lead}
                    </p>
                </Neos.Fusion:Augmenter>
            </div>
        `
    }


}
