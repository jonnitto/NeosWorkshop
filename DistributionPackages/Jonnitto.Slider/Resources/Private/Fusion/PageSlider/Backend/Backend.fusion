prototype(Jonnitto.Slider:PageSlider.Backend) < prototype(Neos.Fusion:Component) {
    @if {
        inBackend = ${node.context.inBackend}
        hasPageSlider = ${q(node).is('[instanceof Jonnitto.Slider:Mixin.PageSlider]')}
    }

    headline = ${Translation.translate('pageSliderTitle', 'Slider on the top of the page', [], null, 'Jonnitto.Slider')}
    namespace = 'slider-edit'

    renderer = afx`
        <div id={props.namespace} class={props.namespace}>
            <h1 class={BEM.string(props.namespace, 'title')}>{props.headline}</h1>
            <Neos.Neos:ContentCollection nodePath='pageslides' attributes.class={BEM.string(props.namespace, 'content')} />
        </div>
    `
}
